
global int $currLayer = -1;

//=====================================================================================================================//
// Initialize																					
//
//=====================================================================================================================//

global proc AEInitializeBuild (string $nodeNameArr) {

	// get node name
	string $buffer[];
	tokenize($nodeNameArr,".",$buffer);
	string $nodeName = $buffer[0];

	//------------------------------------------------------------------------------------------------------------------//
	// Attribute controls and buttons																					//
	//------------------------------------------------------------------------------------------------------------------//

		//--------------------------------------------------------------------------------------------------------------//
		// User instructions / description																				//
		//--------------------------------------------------------------------------------------------------------------//

		separator -style none;
		text -l "To create a LEGO layout of your mesh, first initialize it";
		separator -style none;

	string $initCmd = "editorTemplate -dimControl " + $nodeName + " InitializeButton true; \
					     button -e -enable false InitializeButton; \
					     button -e -enable true ExportButton; \
					     editorTemplate -dimControl " + $nodeName + " ExportButton false; \
					     button -e -enable true StepButton; \
					     editorTemplate -dimControl " + $nodeName + " StepButton false; \
					     setAttr -type \"string\" " + $nodeName + ".stabilityStatus \"Initializing...\";";

	button -label "Initialize" -enable true -c $initCmd InitializeButton;
}

global proc AEInitializeUpdate (string $nodeNameArr) {

	// get node name
	string $buffer[];
	tokenize($nodeNameArr,".",$buffer);
	string $nodeName = $buffer[0];
	string $initCmd = "";

	// dim/undim attribute fields based on nodeStage
	string $stage = `getAttr ($nodeName + ".stabilityStatus")`;
	if ($stage == "Uninitialized") {
		editorTemplate -dimControl $nodeName InitializeButton false;
		button -e -enable true InitializeButton;
		string $initCmd = "editorTemplate -dimControl " + $nodeName + " InitializeButton true; \
					     button -e -enable false InitializeButton; \
					     button -e -enable true ExportButton; \
					     editorTemplate -dimControl " + $nodeName + " ExportButton false; \
					     button -e -enable true StepButton; \
					     editorTemplate -dimControl " + $nodeName + " StepButton false; \
					     setAttr -type \"string\" " + $nodeName + ".stabilityStatus \"Initializing...\";";

	} else { // do nothing, node has already been initialized
		editorTemplate -dimControl $nodeName InitializeButton true;
		button -e -enable false InitializeButton;
		$initCmd = "";
	} 

	button -edit -c $initCmd InitializeButton;
}



//=====================================================================================================================//
// Export																					
//
//=====================================================================================================================//

global proc openFileDialog(string $nodeName) {
	
	string $filename = fileDialog("-dm", "*.pdf", "-mode", 1);
	if ($filename != "")
	{
		setAttr -type "string" ($nodeName + ".exportPath") $filename;
	    setAttr -type "string" ($nodeName + ".stabilityStatus") "Exporting...";
	}

}

global proc AEExportBuild (string $nodeNameArr) {

	// get node name
	string $buffer[];
	tokenize($nodeNameArr,".",$buffer);
	string $nodeName = $buffer[0];

	//------------------------------------------------------------------------------------------------------------------//
	// Attribute controls and buttons																					//
	//------------------------------------------------------------------------------------------------------------------//

		//--------------------------------------------------------------------------------------------------------------//
		// User instructions / description																				//
		//--------------------------------------------------------------------------------------------------------------//

		separator -style none;
		text -l "Export LEGO layout as a PDF";
		separator -style none;

	string $exportCmd = "openFileDialog(\"" + $nodeName + "\");";

	button -label "Export Layout" -enable false -c $exportCmd ExportButton;
}


global proc AEExportUpdate (string $nodeNameArr) {

	// do nothing, same process 
}

//=====================================================================================================================//
// Step Button																				
//
//=====================================================================================================================//

global proc hideLayoutChildren() {
	global int $currLayer;
	if(objExists("legoLayout")) {
		// hide children separately so they show up correctly
		string $children[] = listRelatives("-children","legoLayout");
		for($child in $children) {
			hide($child);
		}
		$currLayer = 0;
	}
}

global proc step(string $nodeName) {
	if(objExists("legoLayout")) {
		global int $currLayer;
		
		// reset if we hit max
		int $maxLayer = getAttr($nodeName + ".maxLayer");
		if($currLayer == $maxLayer - 1) {
		  print("currlayer == max \n");
		  $currLayer = -1;
		  //reset($nodeName);
		}

		if($currLayer == -1) {
			hideLayoutChildren();
		}

		string $layerStr = "legoLayout|layer";
		if ($currLayer < 10) {
			$layerStr += "0";
		} 
		$layerStr += string($currLayer);
		if(objExists($layerStr)) {
			showHidden($layerStr);
		} 
		print("currLayer " + string($currLayer) + "\n");
		$currLayer = $currLayer + 1;
	}
	
}

global proc reset(string $nodeName) {
	global int $currLayer;
	$currLayer = -1;
	//hideLayoutChildren();
	//step($nodeName);
}

global proc showFullLayout() {
	global int $currLayer;
	$currLayer = -1;
	showHidden("legoLayout", "-b");
	if(objExists("legoLayout")) {
		string $children[] = listRelatives("-children","legoLayout");
		for($child in $children) {
		//	showHidden($child);
		}
	}
}

global proc AEStepBuild (string $nodeNameArr) {

	// get node name
	string $buffer[];
	tokenize($nodeNameArr,".",$buffer);
	string $nodeName = $buffer[0];

		//--------------------------------------------------------------------------------------------------------------//
		// User instructions / description																				//
		//--------------------------------------------------------------------------------------------------------------//

		separator -style none;
		text -l "Step through the layout";
		separator -style none;

		separator -style none;
		text -l "Current Layer: " currLayerStr;
		separator -style none;

	string $stepCmd = "step(\"" + $nodeName + "\");";
	string $resetCmd = "reset();";
	string $showFullCmd = "showFullLayout();";

	//button - label "Reset" -enable false -c $resetCmd ResetStepButton;
	button -label "Step" -enable false -c $stepCmd StepButton;
}

global proc AEStepUpdate (string $nodeNameArr) {

	// do nothing, same process 
}

//=====================================================================================================================//
// Constraint Dropdown 																			
//=====================================================================================================================//

global proc string switchColorVal(string $nodeName) {

	string $curr = `getAttr ($nodeName + ".colorConstraint")`;
	if($curr == "HARD") {
		return "SOFT"; 
	} else {
		return "HARD"; 
	}
}

global proc AEColorConstraintBuild (string $nodeNameArr) {

	// get node name
	string $buffer[];
	tokenize($nodeNameArr,".",$buffer);
	string $nodeName = $buffer[0];

	//-----------------------------------------------------------------------------------------------------------------//
	// Attribute controls and buttons																				
	//
	//-----------------------------------------------------------------------------------------------------------------//

    string $onChange = "setAttr -type \"string\" " + $nodeName + ".colorConstraint " + switchColorVal($nodeName);
	optionMenuGrp -label "Color Constraint" -cc $onChange colorMenu;
	menuItem -label "SOFT"; 
	menuItem -label "HARD";  


}

global proc AEColorConstraintUpdate (string $nodeNameArr) {

	// get node name
	string $buffer[];
	tokenize($nodeNameArr,".",$buffer);
	string $nodeName = $buffer[0];

	//-----------------------------------------------------------------------------------------------------------------//
	// Attribute controls and buttons																				
	//
	//-----------------------------------------------------------------------------------------------------------------//

	string $stage = `getAttr ($nodeName + ".colorConstraint")`;
	if ($stage == "HARD") {
		optionMenuGrp -e -value "HARD" colorMenu;
	} else {
		optionMenuGrp -e -value "SOFT" colorMenu;
	}
}


global proc AEBOBNodeTemplate( string $nodeName ) {

	editorTemplate -beginLayout "BOBNode" -collapse false;

	editorTemplate -suppress "caching";
	editorTemplate -suppress "frozen";
	editorTemplate -suppress "nodeState";
	editorTemplate -suppress "colorConstraint";
	editorTemplate -suppress "exportPath";
	editorTemplate -suppress "outputMesh";
	editorTemplate -suppress "inputMesh";
	editorTemplate -suppress "oneXone";
	editorTemplate -suppress "oneXtwo";
	editorTemplate -suppress "oneXthree";
	editorTemplate -suppress "oneXfour";
	editorTemplate -suppress "oneXsix";
	editorTemplate -suppress "oneXeight";
	editorTemplate -suppress "twoXtwo";
	editorTemplate -suppress "twoXthree";
	editorTemplate -suppress "twoXfour";
	editorTemplate -suppress "twoXsix";
	editorTemplate -suppress "twoXeight";

	editorTemplate -addControl "inputMeshName";
	editorTemplate -addControl "meshTexture";
	editorTemplate -addControl "stabilityStatus";

	editorTemplate -addSeparator;

	editorTemplate -beginLayout "Initialization" -collapse false;
		editorTemplate -callCustom "AEInitializeBuild" "AEInitializeUpdate" $nodeName;
	editorTemplate -endLayout;
	
	editorTemplate -addSeparator;

	editorTemplate -beginLayout "Layout Settings" -collapse false;
		editorTemplate -addControl "useMeshColors";
		editorTemplate -callCustom "AEColorConstraintBuild" "AEColorConstraintUpdate" $nodeName;	
	editorTemplate -endLayout;

	editorTemplate -addSeparator;

	editorTemplate -beginLayout "Layer Traversal" -collapse false;
		editorTemplate -callCustom "AEStepBuild" "AEStepUpdate" $nodeName;	
	editorTemplate -endLayout;

	editorTemplate -addSeparator;

	editorTemplate -callCustom "AEExportBuild" "AEExportUpdate" $nodeName;

	editorTemplate -endLayout;
}